# Maintainer: Trevor SANDY <trevor[dot]sandy[at]gmail[dot]com>
#  Last Update: March 20, 2025
#  Copyright (C) 2017 - 2025 by Trevor SANDY
pkgname=lpub3d-ci
pkgver=2.4.9.4373
pkgrel=1
pkgdesc="An LDraw Building Instruction Editor"
url="https://github.com/trevorsandy/lpub3d-ci.git"
arch=('x86_64' 'i686')
license=('GPL3')
makedepends=('iptables' 'glu' 'gcc' 'make' 'ca-certificates' 'llvm' 'mesa')
depends=('unzip' 'qt5-base' 'qt5-tools' 'libpng' 'libjpeg-turbo' 'gl2ps' 'tinyxml' 'minizip' 'freeglut' 'mesa-libgl' 'boost-libs' 'boost' 'openexr' 'libtiff' 'xorg-util-macros' 'libelf' 'llvm-libs')
conflicts=()
replaces=()
backup=()
source=("lpub3d-ci-git.tar.gz" "complete.zip" "lpub3dldrawunf.zip" "tenteparts.zip" "vexiqparts.zip" "povray.tar.gz" "ldview.tar.gz" "ldglite.tar.gz" "version.info")
noextract=("complete.zip" "lpub3dldrawunf.zip" "tenteparts.zip" "vexiqparts.zip" "povray.tar.gz" "ldview.tar.gz" "ldglite.tar.gz")
md5sums=(SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP)

prepare() {
	# location is /usr/src/packages/BUILD/lpub3d-ci/src
	export WD="$PWD"
	export LDRAWDIR="${HOME}/ldraw"
	export PLATFORM_PRETTY="Arch Linux Extra"
	export PLATFORM_CODE="arex"
	export PLATFORM_VER="2025"
	export LPUB3D="${pkgname}"
	export TARGET_CPU="$(uname -m)"
	export LP3D_LOG_PATH="${WD}"
	export LP3D_CPU_CORES="$(nproc)"
	export LP3D_3RD_DIST_DIR="lpub3d_linux_3rdparty"
	cd ${pkgname}-git
	# location is .../src/${pkgname}-git
	# move ldraw archive libraries to extras
	for LDrawLibFile in \
		../complete.zip \
		../lpub3dldrawunf.zip \
		../tenteparts.zip \
		../vexiqparts.zip; do
		if [ -f "${LDrawLibFile}" ]; then
			[ "$(basename ${LDrawLibFile})" = "complete.zip" ] && LibCopy=cp || LibCopy=mv
			${LibCopy} -f ${LDrawLibFile} mainApp/extras/
		fi
	done
	# copy version.info to utilities
	[ -f "../version.info" ] &&	cp -f ../version.info builds/utilities/ || :
	chmod a+x builds/utilities/CreateRenderers.sh && ./builds/utilities/CreateRenderers.sh
	export QT_SELECT=qt5
	export LP3D_BUILD_PKG=yes
	if [ -x /usr/bin/qmake ] ; then
		QMAKE_EXEC=qmake
	elif [ -x /usr/bin/qmake-qt5 ] ; then
		QMAKE_EXEC=qmake-qt5
	fi
	${QMAKE_EXEC} -makefile CONFIG+=release CONFIG+=build_check CONFIG-=debug_and_release CONFIG+=pkg
}

build() {
	cd ${pkgname}-git
	make clean
	make -j${LP3D_CPU_CORES}
	# check lpub3d dependencies
	[[ "${TARGET_CPU}" = "x86_64" || "${TARGET_CPU}" = "aarch64" ]] && \
	LP3D_RELEASE="64bit_release" || LP3D_RELEASE="32bit_release"
	LP3D_SUFFIX=$(cat builds/utilities/version.info | cut -d " " -f 1-2 | sed s/" "//g)
	LP3D_EXE="mainApp/${LP3D_RELEASE}/lpub3d${LP3D_SUFFIX}"
	[ -f "${LP3D_EXE}" ] && ldd ${LP3D_EXE} 2>/dev/null || :
}

package() {
	cd ${pkgname}-git
	make INSTALL_ROOT=${pkgdir} install
}
