# Maintainer: Trevor SANDY <trevor[dot]sandy[at]gmail[dot]com>
#  Last Update: April 01, 2025
#  Copyright (C) 2017 - 2025 by Trevor SANDY
pkgname=lpub3d-ci
pkgver=2.4.9.4373
pkgrel=1
pkgdesc="An LDraw Building Instruction Editor"
url="https://github.com/trevorsandy/lpub3d-ci.git"
arch=('x86_64' 'i686')
license=('GPL3')
makedepends=('glu' 'gcc' 'make')
depends=('unzip' 'qt5-base' 'qt5-tools' 'libpng' 'libjpeg-turbo' 'gl2ps' 'tinyxml' 'minizip' 'freeglut' 'mesa-libgl' 'boost-libs' 'boost' 'openexr' 'libtiff' 'sdl2' 'xorg-util-macros' 'libelf' 'llvm-libs' 'llvm' 'mesa')
conflicts=()
replaces=()
backup=()
source=('lpub3d-ci-git.tar.gz' 'version.info' 'complete.zip' 'lpub3dldrawunf.zip' 'tenteparts.zip' 'vexiqparts.zip' 'povray.tar.gz' 'ldview.tar.gz' 'ldglite.tar.gz')
noextract=('complete.zip' 'lpub3dldrawunf.zip' 'tenteparts.zip' 'vexiqparts.zip' 'povray.tar.gz' 'ldview.tar.gz' 'ldglite.tar.gz')
sha256sums=(
	SKIP
	SKIP
	'a4ab15366d22ae0589901ec15bcc465fd5bb5b6057661b91b89b810f98a56c06'
	'a22da755a9c10cecf37928f4c951be00be9149146914de2a2b7a526906fdfd0e'
	'95b65664c455f4895f480ab8e3ed9e96046ca69ee1b296922d76a369e2cf2846'
	'e2f4114c8ff6bfe7e5a43b6c88fb47f32328ddb147578456ec0825aa76803b20'
	'2793182e702750daa9c3dea19b167c78d004846e44c4ad73b9c3ec34566e1dcb'
	'97d2be96258695dd66304e38f298062b9427ddd3cad71b433441ffd4ee00c28c'
	'a45d36b1f1be24586989fd468ecdeb2591d4dfa8fe001a76882432a2f6c1c0b9')

prepare() {
	# location is /usr/src/packages/BUILD/lpub3d-ci/src
	export WD="$PWD"
	export LDRAWDIR="${HOME}/ldraw"
	export PLATFORM_PRETTY="Arch Linux Extra"
	export PLATFORM_CODE="arex"
	export PLATFORM_VER="2025"
	export LPUB3D="${pkgname}"
	export TARGET_CPU="$(uname -m)"
	export LP3D_LOG_PATH="${WD}"
	export LP3D_CPU_CORES="$(nproc)"
	export LP3D_3RD_DIST_DIR="lpub3d_linux_3rdparty"
	cd ${pkgname}-git
	# location is .../src/${pkgname}-git
	# move ldraw archive libraries to extras
	for LDrawLibFile in \
		../complete.zip \
		../lpub3dldrawunf.zip \
		../tenteparts.zip \
		../vexiqparts.zip; do
		if [ -f "${LDrawLibFile}" ]; then
			[ "$(basename ${LDrawLibFile})" = "complete.zip" ] && LIB_CMD=cp || LIB_CMD=mv
			${LIB_CMD} -f ${LDrawLibFile} mainApp/extras/
		fi
	done
	# copy version.info to utilities
	[ -f "../version.info" ] &&	cp -f ../version.info builds/utilities/ || :
	chmod a+x builds/utilities/CreateRenderers.sh && ./builds/utilities/CreateRenderers.sh
	export QT_SELECT=qt5
	if [ -x /usr/bin/qmake ] ; then
		QMAKE_EXEC=qmake
	elif [ -x /usr/bin/qmake-qt5 ] ; then
		QMAKE_EXEC=qmake-qt5
	fi
	${QMAKE_EXEC} -makefile CONFIG+=release CONFIG+=build_check CONFIG-=debug_and_release CONFIG+=pkg
}

build() {
	cd ${pkgname}-git
	make clean
	make -j${LP3D_CPU_CORES}
	# check lpub3d dependencies
	[[ "${TARGET_CPU}" = "x86_64" || "${TARGET_CPU}" = "aarch64" ]] && \
	LP3D_RELEASE="64bit_release" || LP3D_RELEASE="32bit_release"
	LP3D_SUFFIX=$(cat builds/utilities/version.info | cut -d " " -f 1-2 | sed s/" "//g)
	LP3D_EXE="mainApp/${LP3D_RELEASE}/lpub3d${LP3D_SUFFIX}"
	[ -f "${LP3D_EXE}" ] && ldd ${LP3D_EXE} 2>/dev/null || :
}

package() {
	cd ${pkgname}-git
	make INSTALL_ROOT=${pkgdir} install
}
